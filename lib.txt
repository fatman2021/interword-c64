;---------------------------------------
;        Interword.library
;---------------------------------------

         *= $c000

         jmp init
         jmp setc
         jmp writ
         jmp updt
         jmp atoc
         jmp opnw
         jmp clsw
         jmp invt
         jmp scla
         jmp scup
         jmp scdn
         jmp setx
         jmp ishp

;---------------------------------------

dat80    = $9800
dat40    = dat80+$0400
bitmap   = $a000
screen   = $8c00
scr80    = $9000
pointer  = $8c00
chars    = dat80

adsp     = $80

;---------------------------------------

crx      .byte 0
cry      .byte 0
cadd     .byte 0,0
cpos     .byte 0,0
ch40     .byte 0
sxpos    .byte 0
maxx     .byte 38
misc     .byte 0,0

;---------------------------------------

init     lda #$06
         sta $d020
         sta $d021
         lda #1
         sta $0286
         jsr $e544
         ldx #23
aloop9   txa
         sta btab0,x
         dex
         bpl aloop9
         lda #>bitmap
         sta $fc
         lda #$00
         sta $fb
         ldy #$00
         ldx #$20
         lda #$00
aloop1   sta ($fb),y
         iny
         bne aloop1
         inc $fc
         dex
         bne aloop1
         ldx #$3f
aloop2   lda pointer,x
         sta bitmap+$0780,x
         lda pointer+$40,x
         sta bitmap+$07c0,x
         lda pointer+$80,x
         sta bitmap+$0f80,x
         lda pointer+$c0,x
         sta bitmap+$0fc0,x
         lda pointer+$0100,x
         sta bitmap+$1780,x
         dex
         bpl aloop2
         lda #>screen
         sta $fc
         lda #$00
         sta $fb
         ldy #0
         ldx #4
aloop4   tya
         sta ($fb),y
         iny
         cpy #240
         bne aloop4
         ldy #0
         clc
         lda $fb
         adc #240
         sta $fb
         lda $fc
         adc #0
         sta $fc
         dex
         bne aloop4
         lda #$02
         sta $dd00
         lda #%10000000
         sta $d018
         lda #$07
         sta $d015
         lda #$18
         sta $d000
         sta $d002
         sta $d004
         lda #$32
         sta $d001
         sta $d003
         sta $d005
         lda #$00
         sta $d010
         lda #$1e+adsp
         sta screen+$03f8
         lda #$1f+adsp
         sta screen+$03f9
         lda #$3e+adsp
         sta screen+$03fa
         lda #$3f+adsp
         sta screen+$03fb
         lda #$5e+adsp
         sta screen+$03fc
         lda #$00
         sta $d027
         lda #$0f
         sta $d028
         lda #$08
         sta $d029
         lda #1
         sta $d02a
         sta $d02b
         lda #$20
         ldx #0
aloop5   sta scr80,x
         sta scr80+$0100,x
         sta scr80+$0200,x
         sta scr80+$0300,x
         sta scr80+$0400,x
         sta scr80+$0500,x
         sta scr80+$0600,x
         sta scr80+$0700,x
         inx
         bne aloop5
         ldx #0
         ldy #0
         lda #0
         jsr setc
         ldx #<text1
         ldy #>text1
         lda #1
         jsr writ
         ldx #0
         ldy #1
         lda #1
         jsr setc
         ldx #<text2a
         ldy #>text2a
         lda #0
         jsr writ
         lda #32
         ldx #39
aloop6   sta $6450,x
         dex
         bpl aloop6
         ldx #20
         ldy #1
         lda #0
         jsr setc
         ldx #<text2
         ldy #>text2
         lda #0
         jsr writ
         ldx #0
         ldy #2
         lda #1
         jsr setc
         ldx #<text3
         ldy #>text3
         lda #1
         jsr writ
         ldx #0
         ldy #24
         lda #0
         jsr setc
         ldx #<text6
         ldy #>text6
         lda #0
         jsr writ
         ldx #39
aloop8   lda lntab,x
         sta screen+$03c0,x
         dex
         bpl aloop8
         rts

text1    .text " Interword v1.00  Pr"
         .text "ojekt: Navnl{pound}s      "
         .text "                    "
         .text "RAM:          {cm l}{cm y}{cm u}{cm d}{sh @}{cm f}"
         .byte 0,0
text2a   .text "{222}{cm asterisk}{sh space}{cm k}{cm i}{cm t}{cm d}{cm z}{cm s}{cm p}          "
         .text "            {cm a}{cm e}{cm r}{cm w}{cm h}{cm j}{cm l}{cm y}"
         .byte 0
text2    .text "{sh asterisk} "
         .byte 0,0
text3    .text " {cm pound}{cm m}{cm m}{cm m}{cm m}{cm g}{cm m}{cm m}{cm pound}{sh pound}{cm m}{cm m}{cm m}{cm m}{cm m}{cm m}{cm pound}{cm m}{cm m}"
         .text "{sh pound}{cm m}{cm m}{cm m}{cm m}{cm pound}{cm m}{cm m}{cm m}{cm m}{sh pound}{cm m}{cm m}{cm pound}{cm m}{cm m}{cm m}{cm m}{cm m} "
         .byte 0,0
text4    .byte 0,32,0,0
text5    .text "{cm a}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm j}{cm e}"
text6    .text " X:6   Y:1   S:1  L:"
         .text "1   {cm q} {cm s}{cm s}{cm d}   "
         .byte 0,0
lntab    .byte $f0,$f1,$f2,$ff,$f3,$f4
         .byte $f5,$ff,$f6,$f7,$f8,$ff
         .byte $f9,$fa,$fb,$ff,$fc,$fd
         .byte $fd,$fd,$fd,$fd,$fd,$fd
         .byte $fd,$fd,$fd,$fd,$fd,$fd
         .byte $fd,$fd,$fd,$fd,$fd,$fd
         .byte $fd,$fd,$fe,$ff
cnt      .byte 0

btab0    .byte 0,1,2,3,4,5,6,7,8,9
         .byte 10,11,12,13,14,15,16
         .byte 17,18,19,20,21,22,23
         .byte 24
btab1    .byte $a0,$a1,$a2,$a3,$a5,$a6
         .byte $a8,$a9,$aa,$ab,$ad,$ae
         .byte $b0,$b1,$b2,$b3,$b5,$b6
         .byte $b8,$b9,$ba,$bb,$bd,$be
         .byte $bf
btab2    .byte $00,$40,$80,$c0,$00,$40
         .byte $00,$40,$80,$c0,$00,$40
         .byte $00,$40,$80,$c0,$00,$40
         .byte $00,$40,$80,$c0,$00,$40
         .byte $80
btab3    .byte $90,$90,$90,$90,$91
         .byte $91,$91,$92,$92,$92
         .byte $93,$93,$93,$94,$94
         .byte $94,$95,$95,$95,$95
         .byte $96,$96,$96,$97,$97
btab4    .byte $00,$50,$a0,$f0,$40
         .byte $90,$e0,$30,$80,$d0
         .byte $20,$70,$c0,$10,$60
         .byte $b0,$00,$50,$a0,$f0
         .byte $40,$90,$e0,$30,$80
btab5    .byte $00,$00,$00,$00,$00,$00
         .byte $00,$00,$00,$00,$00,$00
         .byte $00,$00,$00,$00,$00,$00
         .byte $00,$00,$00,$00,$00,$00
         .byte $00,$00,$00,$00,$00,$00
         .byte $00,$00,$01,$01,$01,$01
         .byte $01,$01,$01,$01
btab6    .byte $00,$08,$10,$18,$20,$28
         .byte $30,$38,$40,$48,$50,$58
         .byte $60,$68,$70,$78,$80,$88
         .byte $90,$98,$a0,$a8,$b0,$b8
         .byte $c0,$c8,$d0,$d8,$e0,$e8
         .byte $f0,$f8,$00,$08,$10,$18
         .byte $20,$28,$30,$38

setc     sta ch40
setc2    stx crx
         sty cry
         txa
         clc
         adc btab4,y
         sta cpos
         lda btab3,y
         adc #0
         sta cpos+1
         lda ch40
         cmp #1
         beq sjump1
         txa
         lsr a
         tax
sjump1   lda btab0,y
         tay
         clc
         lda btab2,y
         adc btab6,x
         sta cadd
         lda btab1,y
         adc btab5,x
         sta cadd+1
         rts

revs     .byte 0

writ     lsr a
         ror a
         sta revs
         lda #0
         sta bm2
         lda ch40
         bne cjump1
         jsr wr80
         rts
cjump1   jsr wr40
         rts

atoc     cmp #96
         bcc atjump1
         and #$3f
         ora #$40
         rts
atjump1  and #$3f
         rts

rev1     = $8b

wr80     lda cpos
         sta $f9
         and #$fe
         sta $f7
         lda cpos+1
         sta $fa
         sta $f8
         lda cadd
         sta $fb
         lda cadd+1
         sta $fc
         stx $fd
         sty $fe
w8loop1  ldy #0
         lda ($fd),y
         beq w8jump1
         jsr atoc
         ora revs
         sta ($f9),y
         inc $f9
         bne w8jump1
         inc $fa
w8jump1  inc crx
         lda crx
         and #1
         beq w8jump5
         jmp w8jump2
w8jump5  ldy #0
         sty rev1
         lda ($f7),y
         and #$7f
         tax
         cmp ($f7),y
         beq w8jump9
         lda #$f0
         sta rev1
w8jump9  iny
         lda ($f7),y
         and #$80
         beq w8jumpa
         lda #$0f
         ora rev1
         sta rev1
w8jumpa  lda ($f7),y
         and #$7f
         tay
         jsr up80
         inc $f7
         inc $f7
         bne w8jump2
         inc $f8
w8jump2  inc $fd
         bne w8jump3
         inc $fe
w8jump3  lda crx
         and #1
         bne w8jump7
         ldy #0
         lda ($fd),y
         beq w8jump4
w8jump7  jmp w8loop1
w8jump4  lda $f9
         sta cpos
         lda $fa
         sta cpos+1
         lda $fb
         sta cadd
         lda $fc
         sta cadd+1
         rts

up80     lda chars,y
         lsr a
         lsr a
         lsr a
         lsr a
         ora chars,x
         sta $18
         lda chars+$80,y
         lsr a
         lsr a
         lsr a
         lsr a
         ora chars+$80,x
         sta $19
         lda chars+$0100,y
         lsr a
         lsr a
         lsr a
         lsr a
         ora chars+$0100,x
         sta $1a
         lda chars+$0180,y
         lsr a
         lsr a
         lsr a
         lsr a
         ora chars+$0180,x
         sta $1b
         lda chars+$0200,y
         lsr a
         lsr a
         lsr a
         lsr a
         ora chars+$0200,x
         sta $1c
         lda chars+$0280,y
         lsr a
         lsr a
         lsr a
         lsr a
         ora chars+$0280,x
         sta $1d
         lda chars+$0300,y
         lsr a
         lsr a
         lsr a
         lsr a
         ora chars+$0300,x
         sta $1e
         lda chars+$0380,y
         lsr a
         lsr a
         lsr a
         lsr a
         ora chars+$0380,x
         sta $1f
         ldy #7
p8l1     lda $18,y
         eor rev1
         sta ($fb),y
         cpy bm2
         bne p8j1
         lda #0
         sta rev1
p8j1     dey
         bpl p8l1
         clc
         lda $fb
         adc #8
         sta $fb
         lda $fc
         adc #0
         sta $fc
         rts

wr40     lda cpos
         sta $f9
         lda cpos+1
         sta $fa
         lda cadd
         sta $fb
         lda cadd+1
         sta $fc
         stx $fd
         sty $fe
         lda #39
         sta edge
         lda cry
         cmp #3
         bcs w4jump5
         inc edge
w4jump5  lda #0
         sta rev1
         lda revs
         beq w4loop1
         lda #$ff
         sta rev1
w4loop1  ldy #0
         lda ($fd),y
         jsr atoc
         ora revs
         sta ($f9),y
         inc $f9
         bne w4jump1
         inc $fa
w4jump1  inc crx
         and #$7f
         tax
         jsr up40
         lda crx
         cmp edge
         beq w4jump3
         ldy #0
         lda ($fd),y
         bne w4loop1
w4jump3  lda $f9
         sta cpos
         lda $fa
         sta cpos+1
         lda $fb
         sta cadd
         lda $fc
         sta cadd+1
         rts

edge     .byte 0

up40     lda rev1
         sta misc
         lda bm2
         cmp #7
         bne p4j1
         lda #0
         sta rev1
p4j1     ldy #0
         lda chars+$0400,x
         eor rev1
         sta ($fb),y
         iny
         lda chars+$0480,x
         eor rev1
         sta ($fb),y
         iny
         lda chars+$0500,x
         eor rev1
         sta ($fb),y
         iny
         lda chars+$0580,x
         eor rev1
         sta ($fb),y
         iny
         lda chars+$0600,x
         eor rev1
         sta ($fb),y
         iny
         lda chars+$0680,x
         eor rev1
         sta ($fb),y
         iny
         lda chars+$0700,x
         eor rev1
         sta ($fb),y
         lda misc
         sta rev1
         iny
         lda chars+$0780,x
         eor rev1
         sta ($fb),y
         clc
         lda $fb
         adc #$08
         sta $fb
         lda $fc
         adc #0
         sta $fc
         inc $fd
         bne w4jump2
         inc $fe
w4jump2  rts

xy       .byte 0,0
xysz     .byte 0,0

updt     sta ch40 ;[x,y,xsz,ysz]
         stx $8b
         sty $8c
         ldy #0
         lda ($8b),y
         sta xy
         iny
         lda ($8b),y
         sta xy+1
         iny
         lda ($8b),y
         sta xysz
         iny
         lda ($8b),y
         sta xysz+1
         lda ch40
         beq ujump1
         jmp updt40
ujump1   jmp updt80

cd       .byte 0,0

updt80   lda xysz+1
         sta cd+1
u8loop2  ldx xy
         ldy xy+1
         jsr setc2
         jsr setbm
         lda cadd
         sta $fb
         lda cadd+1
         sta $fc
         lda cpos
         sta $fd
         lda cpos+1
         sta $fe
         lda xysz
         sta cd
u8loop1  lda #0
         ldy cry
         cpy #2
         bne u8jump2
         lda #$ff
u8jump2  sta rev1
         ldy #0
         sty rev1
         lda ($fd),y
         cmp #$80
         beq u8j1
         cmp #0
         bpl u8j1
         ldx #$f0
         stx rev1
u8j1     and #$7f
         tax
         iny
         lda ($fd),y
         cmp #$80
         beq u8j2
         cmp #0
         bpl u8j2
         pha
         lda rev1
         ora #$0f
         sta rev1
         pla
u8j2     and #$7f
         tay
         jsr up80
         inc $fd
         inc $fd
         bne u8jump1
         inc $fe
u8jump1  dec cd
         dec cd
         bne u8loop1
         inc xy+1
         dec cd+1
         bne u8loop2
         rts

bm2      .byte 0

setbm    ldx #7
         lda xy+1
         cmp #3
         bcs sbj1
         ldx #0
sbj1     stx bm2
         rts

updt40   lda xysz+1
         sta cd+1
u4loop2  ldx xy
         ldy xy+1
         jsr setc2
         jsr setbm
         lda cadd
         sta $fb
         lda cadd+1
         sta $fc
         lda cpos
         clc
         adc fx
         sta $fd
         lda cpos+1
         adc #0
         sta $fe
         lda xysz
         sta cd
u4loop1  lda #0
         ldy cry
         cpy #2
         bne u4jump2
         lda #$ff
u4jump2  sta rev1
         ldy #0
         lda ($fd),y
         cmp #$80
         beq u4j1
         cmp #0
         bpl u4j1
         ldx #$ff
         stx rev1
u4j1     and #$7f
         tax
         jsr up40
         dec cd
         bne u4loop1
         inc xy+1
         dec cd+1
         bne u4loop2
         rts

bufr     .text "                    "
         .text "                    "
         .text "                    "
         .text "                    "
rflg     .byte 0
add      .byte 0
rpt      .byte 0

opnw     stx $2b  ;[x,y,xsz,ysz,lines]
         sty $2c
         lda #0
         sta bm2
         ldx #$3f
oloop4   lda bitmap+$0140,x
         sta $0400,x
         lda bitmap+$0180,x
         sta $0440,x
         lda bitmap+$01c0,x
         sta $0480,x
         lda bitmap+$0200,x
         sta $04c0,x
         lda bitmap+$0240,x
         sta $0500,x
         dex
         bpl oloop4
         ldy #0
         lda ($2b),y
         sta xy
         iny
         lda ($2b),y
         sta xy+1
         iny
         lda ($2b),y
         sta xysz
         iny
         lda ($2b),y
         sta xysz+1
         lda xysz+1
         sta cd+1
         clc
         lda $2b
         adc #4
         sta $2b
         lda $2c
         adc #0
         sta $2c
         lda #$80
         sta rflg
oloop1   ldx #0
         ldy #0
         sty rpt
oloop3   lda ($2b),y
         cmp #64
         bne ojump6
         iny
         lda ($2b),y
         sec
         sbc #1
         sta rpt
         iny
         jmp oloop3
ojump6   cmp #18
         bne ojump1
         lda rflg
         eor #$80
         sta rflg
         jmp ojump2
ojump1   jsr atoc
         ora rflg
         sta bufr,x
         inx
ojump2   lda rpt
         bne ojump7
         iny
         jmp ojump8
ojump7   dec rpt
ojump8   cpx xysz
         bne oloop3
         sty add
         ldx xy
         ldy xy+1
         lda #0
         jsr setc
         lda #0
         sta cd
         lda cadd
         sta $fb
         lda cadd+1
         sta $fc
oloop2   lda #0
         sta rev1
         ldy cd
         lda bufr,y
         and #$7f
         tax
         cmp bufr,y
         beq ojump4
         lda #$f0
         sta rev1
ojump4   iny
         lda bufr,y
         and #$80
         beq ojump3
         lda #$0f
         ora rev1
         sta rev1
ojump3   lda bufr,y
         and #$7f
         tay
         jsr up80
         inc cd
         inc cd
         lda cd
         cmp xysz
         bne oloop2
         clc
         lda $2b
         adc add
         sta $2b
         lda $2c
         adc #0
         sta $2c
         inc xy+1
         dec cd+1
         beq ojump5
         jmp oloop1
ojump5   ldx xy
         ldy xysz
         lda xysz+1
         rts

cwdat    .byte 0,0,0,0

clsw     stx $2b
         sty $2c
         sta ch40
         ldy #0
         lda ($2b),y
         sta cwdat
         iny
         lda ($2b),y
         sta cwdat+1
         iny
         lda ($2b),y
         sta cwdat+2
         iny
         lda ($2b),y
         sta cwdat+3
         lda ch40
         beq cwjump1
         lsr cwdat
         lsr cwdat+2
cwjump1  lda cwdat+1
         cmp #1
         bne cwjump2
         inc cwdat+1
         dec cwdat+3
cwjump2  ldx #$3f
cwloop1  lda $0400,x
         sta bitmap+$0140,x
         lda $0440,x
         sta bitmap+$0180,x
         lda $0480,x
         sta bitmap+$01c0,x
         lda $04c0,x
         sta bitmap+$0200,x
         lda $0500,x
         sta bitmap+$0240,x
         dex
         bpl cwloop1
         lda cwdat+3
         beq cwjump3
         ldx #<cwdat
         ldy #>cwdat
         lda ch40
         jsr updt
cwjump3  rts

range    .byte 0
xst      .byte 0
exor     .byte 0

invt     sta range
         stx xst
         lda #0
         jsr setc
         lda cadd
         sta $fb
         lda cadd+1
         sta $fc
         lda xst
         and #1
         beq ijump1
         ldy blkmd
iloop1   lda ($fb),y
         eor #$0f
         sta ($fb),y
         iny
         cpy #8
         bne iloop1
         clc
         lda $fb
         adc #8
         sta $fb
         lda $fc
         adc #0
         sta $fc
         dec range
         beq ijump3
ijump1   lda #$ff
         ldx range
         cpx #1
         bne ijump2
         lda #$f0
         inc range
ijump2   ldy blkmd
         sta exor
iloop2   lda ($fb),y
         eor exor
         sta ($fb),y
         iny
         cpy #8
         bne iloop2
         clc
         lda $fb
         adc #8
         sta $fb
         lda $fc
         adc #0
         sta $fc
         dec range
         dec range
         bne ijump1
ijump3   rts

scla     inx
         inx
         inx
         lda btab4,x
         ldy btab3,x
         tax
         rts

scup     stx lin
         stx stlin
         lda ciff,x
         sta cf
         lda btab0,x
         sta wrap
         lda btab7,x
         sta $fc
         lda btab8,x
         sta $fb
suloop1  ldx lin
         lda btab0+1,x
         sta btab0,x
         ldy cf
         cpy #5
         bne sujump1
         lda wrap
         sta btab0,x
         lda btab0+1,x
         sta wrap
         cpx #23
         beq sujump1
         ldy btab0,x
         lda btab1,y
         sta $fa
         lda btab2,y
         sta $f9
         inx
         ldy btab0,x
         lda btab1,y
         sta $fe
         lda btab2,y
         sta $fd
         dex
         ldy #0
suloop3  lda ($fd),y
         sta ($f9),y
         iny
         bne suloop3
         ldy #$37
         inc $fe
         inc $fa
suloop4  lda ($fd),y
         sta ($f9),y
         dey
         bpl suloop4
sujump1  lda btab0,x
         tax
         ldy ciff,x
         ldx btab8,y
         ldy #0
suloop2  txa
         sta ($fb),y
         inx
         iny
         cpy #39
         bne suloop2
         clc
         lda $fb
         adc #40
         sta $fb
         lda $fc
         adc #0
         sta $fc
         inc cf
         lda cf
         cmp #6
         bne sujump2
         lda #0
         sta cf
sujump2  ldx lin
         inx
         stx lin
         cpx #24
         beq sujump3
         jmp suloop1
sujump3  ldx stlin
         lda btab3,x
         sta $fc
         lda btab4,x
         sta $fb
         inx
         lda btab3,x
         sta $fe
         lda btab4,x
         sta $fd
suloop6  ldy #78
suloop5  lda ($fd),y
         sta ($fb),y
         dey
         bpl suloop5
         clc
         lda $fb
         adc #80
         sta $fb
         lda $fc
         adc #0
         sta $fc
         lda $fd
         adc #80
         sta $fd
         lda $fe
         adc #0
         sta $fe
         inx
         cpx #24
         bne suloop6
         rts

cf       .byte 0
wrap     .byte 0
ciff     .byte 0,1,2,3,4,5
         .byte 0,1,2,3,4,5
         .byte 0,1,2,3,4,5
         .byte 0,1,2,3,4,5
lin      .byte 0
stlin    .byte 0
btab7    .byte $8c,$8c,$8c,$8c,$8c,$8c
         .byte $8c,$8d,$8d,$8d,$8d,$8d
         .byte $8d,$8e,$8e,$8e,$8e,$8e
         .byte $8e,$8e,$8f,$8f,$8f,$8f
         .byte $8f
btab8    .byte $00,$28,$50,$78,$a0,$c8
         .byte $f0,$18,$40,$68,$90,$b8
         .byte $e0,$08,$30,$58,$80,$a8
         .byte $d0,$f8,$20,$48,$70,$98
         .byte $c0
sds      .byte 0

scdn     stx sds
         ldx #23
         stx lin
         lda ciff,x
         sta cf
         lda btab0,x
         sta wrap
         lda btab7,x
         sta $fc
         lda btab8,x
         sta $fb
sdloop1  ldx lin
         lda btab0-1,x
         sta btab0,x
         cpx sds
         bne sdjump4
         lda wrap
         sta btab0,x
sdjump4  ldy cf
         cpy #0
         bne sdjump1
         lda wrap
         sta btab0,x
         lda btab0-1,x
         sta wrap
         ldy btab0,x
         lda btab1,y
         sta $fa
         lda btab2,y
         sta $f9
         dex
         ldy btab0,x
         lda btab1,y
         sta $fe
         lda btab2,y
         sta $fd
         inx
         ldy #0
sdloop3  lda ($fd),y
         sta ($f9),y
         iny
         bne sdloop3
         ldy #$37
         inc $fe
         inc $fa
sdloop4  lda ($fd),y
         sta ($f9),y
         dey
         bpl sdloop4
sdjump1  lda btab0,x
         tax
         ldy ciff,x
         ldx btab8,y
         ldy #0
sdloop2  txa
         sta ($fb),y
         inx
         iny
         cpy #39
         bne sdloop2
         sec
         lda $fb
         sbc #40
         sta $fb
         lda $fc
         sbc #0
         sta $fc
         dec cf
         bpl sdjump2
         lda #5
         sta cf
sdjump2  ldx lin
         cpx sds
         beq sdjump3
         dex
         stx lin
         jmp sdloop1
sdjump3  ldx #22
         lda btab3,x
         sta $fc
         lda btab4,x
         sta $fb
         inx
         lda btab3,x
         sta $fe
         lda btab4,x
         sta $fd
         ldx #24
sdloop5  ldy #78
sdloop6  lda ($fb),y
         sta ($fd),y
         dey
         bpl sdloop6
         sec
         lda $fb
         sbc #80
         sta $fb
         lda $fc
         sbc #0
         sta $fc
         lda $fd
         sbc #80
         sta $fd
         lda $fe
         sbc #0
         sta $fe
         dex
         cpx sds
         bne sdloop5
         rts

fx       .byte 0

setx     stx fx
         rts

blkmd    .byte 0

ishp     stx blkmd
         rts
